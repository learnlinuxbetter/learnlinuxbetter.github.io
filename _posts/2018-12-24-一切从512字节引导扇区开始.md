---
title: 一切从512字节引导扇区开始
date: 2018-12-24 00:00:00
categories: QIUOS
tags:

---

在开始这一章之前我们先回顾一下计算机开机引导的流程：

## 一、开机引导流程

对于80386之后的机器，我们按下计算机电源键开始，CPU初始化CS寄存器为0xF000，CS的段描述符高速缓冲器为0xFFFF0000，IP寄存器为0xFFF0。

![img](https://qiutianshu.files.wordpress.com/2018/12/%E6%B7%B1%E5%BA%A6%E6%88%AA%E5%9B%BE_deepin-terminal_20181224114253.png?w=700)

<p style="text-align: center">开机时段寄存器状态</p>


cpu此时执行0xFFFF0000+0xFFF0=0xFFFFFFF0处的指令，该处是一个跳转指令用于清空段寄存器并且跳转到0xF0000，此处是BIOS程序开始的地方。也许你会纳闷0xFFFFFFF0不是已经大于1MB内存空间了么，怎么还可以寻得到呢？其实cpu在实模式下将此处映射到了ROM的1MB内存范围内也就是1MB往下16字节的地方，此处正好是BIOS的第一条指令jmp，通过这个jmp就进入了BIOS主程序。

此时你也许还会问为什么不是直接用实模式下的寻址方式，取CS的值左移4位加上IP的值得到0xFFFF0呢？这里有个**不太常见的知识**，这也是我通过调试发现的。其实每个段寄存器都有一个只能被cpu内部访问“影子寄存器”，也就是“段描述符高速缓冲器”，这个寄存器是干嘛用的呢？我们无论是在实模式下还是在保护模式下，如果每次访存都要使用段部件进行地址转换的话那么效率实在是太低了，于是就给每个段寄存器附加了一个64位的“影子寄存器”用于缓存，这样以来只要没有发生段间的转移，可以直接用“影子寄存器”的值加上偏移就可以访存啦。**所以我们写汇编的时候看似是在使用段寄存器，实际上我们访问的是“影子寄存器”**。更多具体的知识后面解释。

回到开机启动的话题，BIOS首先完成计算机硬件的自检（Power-On Self-Test）确保硬件满足运行的需求。

![img](https://qiutianshu.files.wordpress.com/2018/12/bg2013021502.jpg?w=395&h=328)
<p style="text-align: center">BIOS芯片</p>

自检完成后根据BIOS里面设置的开机引导顺序依次检查寻找可启动的设备，读取每个设备的第一个扇区，如果该扇区以0xAA55结束则说明该扇区为引导扇区，也称为主引导记录（MBR），并将主引导记录加载到内存中0x7C00的位置，如果不是引导扇区则读取下一个设备。以下是主引导记录的结构：

```
　　（1） 第1-446字节：调用操作系统的机器码

　　（2） 第447-510字节：分区表（Partition table）

　　（3） 第511-512字节：主引导记录签名（0x55和0xAA）
```

加载完主引导记录，BIOS执行一个跳转指令跳转到0x7C00处，此时就开始执行调用操作系统的机器码。此机器码根据分区表寻找内核加载器（Loader）的位置，并将Loader从硬盘加载进入内存。

加载完Loader之后执行一个长跳转指令跳到Loader运行，Loader同样也是根据分区表寻找并加载操作系统内核（Kernel）到内存的某一位置，同时设置并加载全局描述符表（GDT），整个系统便从实模式进入了保护模式，而后设置内存分页，将内核按照分页后的内存布局重新映射各个段的内容，最后执行长跳转，内核正式登基！

这里为什么要通过Loader来加载内核而不是直接用主引导记录来干这个活呢？别忘了主引导扇区只有512字节，这么点空间完成那么多任务是几乎不可能的，所以主引导记录完成简单的操作后，剩下的大部分都交给Loader来完成。

## 二、FAT12文件系统

FAT12是早期DOS系统使用的文件系统，这里介绍FAT12是因为QiuOS早期是从1.44MB软盘引导启动的，随着后期功能的完善，当有了自己的文件系统后才改由硬盘启动，所以这里有必要先了解下FAT12文件系统。

### 2.1 磁盘管理层次

为方便存储管理FAT12将磁盘划分为一下层次

- 扇区（Sector）：磁盘最小存取单元，所有的存取都扇区的整数倍
- 簇（Cluster）：一个或多个扇区
- 分区（Partition）：一个分区就包含了完整的文件系统

### 2.2 软盘数据结构

![img](https://qiutianshu.files.wordpress.com/2018/12/%E6%9C%AA%E5%91%BD%E5%90%8D%E6%96%87%E4%BB%B6.png?w=340&h=407)

<p style="text-align: center">1.44软盘数据结构</p>

引导扇区是整个软盘的第0个扇区，包含了元数据（Metadata）、引导程序、分区表、引导扇区标志。

| **名称**        | **偏移** | **长度** | **内容**                      | **值**               |
| --------------- | -------- | -------- | ----------------------------- | -------------------- |
| BS_jmpBoot      | 0        | 3        | 跳转指令（必须填满3字节）     | jmp LABEL_START nop  |
| BS_OEMName      | 3        | 8        | 厂商名                        | “QiuOS ” （3个空格） |
| BPB_BytesPerSec | 11       | 2        | 每扇区字节数                  | 0x200                |
| BPB_SecPerClus  | 13       | 1        | 每簇扇区数                    | 1                    |
| BPB_RsvdSecCnt  | 14       | 2        | Boot记录占用多少扇区          | 1                    |
| BPB_NumFATs     | 16       | 1        | FAT表数量                     | 2                    |
| BPB_RootEntCnt  | 17       | 2        | 根目录最大文件数              | 0xE0                 |
| BPB_TotSec16    | 19       | 2        | 扇区总数                      | 0xB40                |
| BPB_Media       | 21       | 1        | 介质描述符                    | 0xF0                 |
| BPB_FATSz16     | 22       | 2        | FAT表扇区数                   | 9                    |
| BPB_SecPerTrk   | 24       | 2        | 每磁道扇区数                  | 0x12                 |
| BPB_NumHeads    | 26       | 2        | 磁头数                        | 2                    |
| BPB_HiddSec     | 28       | 4        | 隐藏扇区数                    | 0                    |
| BPB_TotSec32    | 32       | 4        | 如果BPB_TotSec16为0则使用此值 | 0                    |
| BS_DrvNum       | 36       | 1        | 中断13对应的驱动器号          | 0                    |
| BS_Resverd1     | 37       | 1        | 保留                          | 0                    |
| BS_BootSig      | 38       | 1        | 扩展引导标记                  | 0x29                 |
| BS_VolID        | 39       | 4        | 卷序列号                      | 0                    |
| BS_VolLab       | 43       | 11       | 卷标                          | “QiuOS 0.11 ”(1空格) |
| BS_FileSysType  | 54       | 8        | 文件系统类型                  | “FAT12 ”(3空格)      |
| 引导代码        | 62       | 385      | 引导代码                      |                      |
| 分区表          | 447      | 63       | 软盘分区表结构                |                      |
| 结束标志        | 510      | 2        | 引导扇区结束标志              | 0xAA55               |

<p style="text-align: center">引导扇区结构</p>

引导扇区后面跟着FAT表，它是一个位图结构，其作用是当数据长度大于1个簇的时候指向下一个数据簇的簇号，起到的相当于指针的作用。FAT2与FAT1内容相同互为备份。

FAT表后为根目录区，起始扇区号为19，它由若干个目录项组成，最大项数为BPB_RootEntCnt，所以其大小不固定。

| **名称**     | **偏移** | **长度** | **描述**                 |
| ------------ | -------- | -------- | ------------------------ |
| DIR_Name     | 0        | 0xB      | 文件名8字节，扩展名3字节 |
| DIR_Attr     | 0xB      | 1        | 文件属性                 |
| 保留位       | 0xC      | 0x10     | 保留位                   |
| DIR_WrtTime  | 0x16     | 2        | 最后一次写入时间         |
| DIR_WrtDate  | 0x18     | 2        | 最后一次写入日期         |
| DIR_FstClus  | 0x1A     | 2        | 此条目对应的开始簇号     |
| DIR_FileSize | 0x1C     | 4        | 文件大小                 |

<p style="text-align: center">根目录条目数据结构</p>


### 2.3 文件索引流程

接下来我们通过一个例子来弄明白文件索引的整个流程：

我们要查找并将a.img中的内核加载器loader.bin加载进入内存，首先计算根目录区的起始扇区号为19（引导扇区+FAT1+FTA2），所以其第一个字节为19 × 512 = 0x2600。

![img](https://qiutianshu.files.wordpress.com/2018/12/%E6%B7%B1%E5%BA%A6%E6%88%AA%E5%9B%BE_%E9%80%89%E6%8B%A9%E5%8C%BA%E5%9F%9F_20181224172141.png?w=700)

<p style="text-align: center">根目录区</p>


由根目录区得到loader.bin在数据区的起始簇号为0x003，大小为0x0A7A，这里需要注意的是数据区的起始簇号是2，也就是说这个文件起始于数据区第2个簇。

数据区第一个簇，也就是第一个扇区的扇区号为：根目录扇区起始号+根目录扇区数，根目录起始扇区为19。

```asm
SectNumOfRootDir = (32 × BPB_RootEntCnt) / BPB_BytesPerSec 
如果余数不为0则：
SectNumOfRootDir = (32 × BPB_RootEntCnt) / BPB_BytesPerSec + 1
```

这里我们可以算出SectNumOfRootDir = 14，所以数据区起始扇区号为19+14=33那么我们的loader.bin的起始扇区号为34，也就是偏移为33 × 512 = 0x4400处。

当我们的文件大于一个簇的时候，就要通过FAT表来找到下一个文件簇的扇区号。FAT表中每12位为一个FAT项，代表下一个数据簇的簇号。第0和第1个FAT项保留，也就是说数据区FAT项从第3个字节开始（从0开始计），这正好与数据区簇号从2开始相对应。

![img](https://qiutianshu.files.wordpress.com/2018/12/%E6%B7%B1%E5%BA%A6%E6%88%AA%E5%9B%BE_%E9%80%89%E6%8B%A9%E5%8C%BA%E5%9F%9F_20181224174131.png?w=700)
<p style="text-align: center">FAT1</p>


loader.bin数据区起始簇号为3，通过查FAT表可知其下一个簇的簇号为0x01E。如果值大于或等于0xFF8则代表这是最后一个簇，如果值为0xFF7则代表这是一个坏簇。

这里需要注意一下FAT表项的计算方法，连续3个字节中，低字节为前一个FAT项的低8位，中间字节的低4位为前一个FAT项的高4位。中间字节的高4位为后一个FAT项的低4位，最后一个字节为后一个FAT项的高8位。这里需要注意的是一个FAT项可能跨两个扇区，因此我们在读FAT表时，每次需要读入2个扇区。

## 三、BOOT.ASM

前面介绍了这么多终于来到引导程序。

```asm
org 0x7c00

	jmp LABEL_START

...

LABEL_START:
	mov ax,cs                               
	mov ds,ax
	mov es,ax
	mov ss,ax
	mov sp,TopOfStack-1                    

	xor ah,ah			
	xor dl,dl	
	int 13h										
```
<p style="text-align: center">寄存器初始化</p>

开头首先初始化段寄存器，指定栈顶。由于代码段从0x7C00开始向高地址增长，所以栈顶设为0x7C00正好位于代码段之下。调用13h中断初始化软驱位置，为读扇区准备。

```asm
ReadSector:
	push bp
	mov bp,sp
	sub sp,1								
	mov byte [bp-1],cl 						
	push bx									 
            
	mov bl,[BPB_SECPerTrk]					 
	div bl
	inc ah
	mov cl,ah		;起始扇区号						 
  
	mov ch,al								
	shr ch,1		;柱面号						 
 
	mov dh,al
	and dh,1		;磁头号						 
	pop bx 									 

.read:
	mov ah,2
	mov al,[bp-1]		;读取扇区数					 

	mov dl,0                ;驱动器号，0表示软驱
	int 13h
	jc .read 		;读取过程中发生错误就一直循环读取						 
 
	add sp,1
	pop bp

	ret
```

ReadSector函数读取指定位置和数量的数据块进入内存。 AX寄存器存放逻辑扇区号，CL存放要读取的扇区数，ES:BX指向缓冲区。

| **寄存器**                                                   | **作用**                          |
| ------------------------------------------------------------ | --------------------------------- |
| AH=00h DL=驱动器号                                           | 复位软驱                          |
| AH=02h AL=要读取的扇区数 CH=柱面号 CL=起始扇区号 DH=磁头号 DL=驱动器号 ES:BX ——>缓冲区 | 从磁盘读取数据到ES:BX指向的缓冲区 |

<p style="text-align: center">13h中断使用方法</p>

要使用13h中断读取扇区，需要将逻辑扇区号转化为柱面号、磁头号、起始扇区号。

![img](https://qiutianshu.files.wordpress.com/2018/12/%E6%9C%AA%E5%91%BD%E5%90%8D%E6%96%87%E4%BB%B6-1-1.png?w=425&h=223)
<p style="text-align: center">CHS计算方法</p>

起始扇区号是从1开始计算，所以余数需要加1。偶数号磁头读偶数号磁道，奇数号磁头读奇数号磁道，所以柱面的磁道是正反两面交错排列的。

```asm
SectorNoOfFAT:
	push bp
	mov bp,sp
	sub sp,2
	push es
	push ax
	mov ax,BaseOfLoader
	sub ax,0x100
	mov es,ax
	pop ax                          

	xor dx,dx
	mov bx,3
	mul bx
	mov bx,2                       ;(簇号 × 3)/2
	div bx 							 
 
	cmp dx,0 
	je EVEN                        ;余数为0，读取前12字节
	mov byte [bp-2],1					 
	jmp NEXT
EVEN:
	mov byte [bp-2],0					 
 
NEXT:
	xor dx,dx
	mov bx,512
	div bx 			    ;ax<-商  FAT项相对于FAT扇区号	 
	push dx                     ;dx<-余数 FAT项在扇区内的偏移   
	add ax,1 						 
	mov bx,0						 
	mov cl,2		    ;一次读2个扇区			 
 
	call ReadSector

	pop dx
	add bx,dx
	mov ax,[es:bx]
	cmp byte [bp-2],1
	jnz EVEN2
	shr ax,4
EVEN2:
	and ax,0xfff
	pop es
	add sp,2
	pop bp
	ret
```

SectorNoOfFAT函数根据数据块的簇号查找FAT表，得到其下一个簇的簇号。AX寄存器存放当前数据块的簇号，查找结果也通过AX寄存器返回。这里为了防止FAT项跨扇区，每次都是读取2个扇区进入缓冲区。

```asm
LABEL_START:

...

  mov byte [wSectorNo],SectorNoOfRootDir;根目录扇区号
  mov word [wNumOfSector],14		;根目录扇区数
.1:
   cmp word [wNumOfSector],0
   jz .FILE_NOT_FOUND			;搜索完毕

   dec word [wNumOfSector]
   mov ax,BaseOfLoader
   mov es,ax
   mov bx,OffsetOfLoader
   mov ax,[wSectorNo]
   mov cl,1				;读取1个根目录扇区到es:bx
   call ReadSector							

   mov si,FileName	                ;ds:si->文件名
   mov di,OffsetOfLoader 		;es:di
   mov dl,0x10 				;每个扇区的条目数
.4:
   cmp dl,0									 
   je .NEXT_SECTOR
   mov cx,0xb		
   dec dl							
   cld
.3:
   cmp cx,0
   je .FILE_FOUND 			;找到文件
   lodsb				;ds:si->al
   cmp al,byte [es:di]
   jne .NEXT_ENTRY
   inc di
   dec cx
   jmp .3

.NEXT_ENTRY:
   and di,0xffe0		        ;回到本条目
   add di,0x20				;下一个条目
   mov si,FileName	
   jmp .4

.NEXT_SECTOR:
   inc byte [wSectorNo]			;下一个扇区
   jmp .1

.FILE_NOT_FOUND:
   push NotFound
   call DispStr
   add sp,2
   jmp $

.FILE_FOUND:
```
<p style="text-align: center">搜索loader.bin文件</p>


主函数中，每次读取根目录区的1个扇区到缓冲区BaseOfLoader:OffsetOfLoader以遍历搜索loader.bin。wNumOfSector变量记录了根目录中尚未读取的扇区数。这里暂时先把Loader的缓冲区用来装载根目录条目，一旦找到了Loader的位置则立即用loader.bin覆盖掉。

```asm
        and di,0xffe0
	add di,0x1a		
	mov cx,[es:di]		;此条目对应的起始簇号					
	mov word [wNextDataBlock],OffsetOfLoader

NEXT_DATA_BLOCK:
        mov ah,0xe 
	mov al,"."              ;每加载一个扇区打印一个“.”
	mov bl,0xf 
	int 10h

	push cx			;暂存扇区
	add cx,31		;簇号+31=逻辑扇区号						
	mov ax,BaseOfLoader
	mov es,ax
	mov bx,[wNextDataBlock] ;wNextDataBlock每次循环自增512
	mov ax,cx

	mov cl,1		;读取数据区第一个扇区
	call ReadSector								 
     
	pop ax                  ;计算下一个数据块的簇号
	call SectorNoOfFAT
	cmp ax,0xff0
	jnb LOAD_OK		;最后一个簇加载完毕
	mov cx,ax               ;下一个数据块簇号
	add word [wNextDataBlock],0x200
	jmp NEXT_DATA_BLOCK
```
<p style="text-align: center">加载loader.bin</p>

由于簇号都是相对于数据区而言，而且都是从2开始，而ReadSector需要的是逻辑扇区号，所以每次都需要将簇号加上31，这样就变成了逻辑扇区号。

```asm
LOAD_OK:
	jmp BaseOfLoader:OffsetOfLoader
```

最后当loader.bin加载完毕就通过长跳转跳到Loader执行，剩下的工作就交给Loader了。